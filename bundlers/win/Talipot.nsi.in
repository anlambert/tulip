!define PYTHON_INSTALLER_EXT "@PYTHON_INSTALLER_EXT@"

!include "MUI.nsh"
!include "x64.nsh"
!include "FileAssociation.nsh"

Function .onInit
  ${If} ${RunningX64}
   Goto Yes
  ${Else}
    MessageBox MB_OK "You cannot install the 64 bit version of Talipot @TalipotVersion@ on a Windows 32 bit system."
    Abort
  ${Endif}
Yes:
FunctionEnd
  InstallDir "$PROGRAMFILES64\Talipot-@TalipotVersion@"
  Name "Talipot-@TalipotVersion@ (x64)"

!define MUI_ICON "files\share\talipot\bitmaps\talipot.ico"
!define MUI_UNICON "files\share\talipot\bitmaps\talipot.ico"

RequestExecutionLevel admin

!define MUI_ABORTWARNING

; Installer pages
!insertmacro MUI_PAGE_LICENSE "LICENSE"
!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_INSTFILES

; Uninstaller pages
!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES

; Language
!insertmacro MUI_LANGUAGE "English"

; Setup file
!ifndef OUT_FILE
!define OUT_FILE "@CMAKE_BINARY_DIR@/Talipot-@TalipotVersion@_x64@SUFFIX@_setup.exe"
!endif
OutFile "${OUT_FILE}"

!define SHCNE_ASSOCCHANGED 0x08000000
!define SHCNF_IDLIST 0

Function RefreshShellIcons
  ; By jerome tremblay - april 2003
  System::Call 'shell32.dll::SHChangeNotify(i, i, i, i) v \
  (${SHCNE_ASSOCCHANGED}, ${SHCNF_IDLIST}, 0, 0)'
FunctionEnd

Function un.RefreshShellIcons
  ; By jerome tremblay - april 2003
  System::Call 'shell32.dll::SHChangeNotify(i, i, i, i) v \
  (${SHCNE_ASSOCCHANGED}, ${SHCNF_IDLIST}, 0, 0)'
FunctionEnd

Section "Talipot"
  SetShellVarContext all
  SetOutPath $INSTDIR
  SetRegView 64

  File /r files\*.*

  CreateDirectory "$SMPROGRAMS\Talipot-@TalipotVersion@ (x64)"
  CreateShortCut "$SMPROGRAMS\Talipot-@TalipotVersion@ (x64)\Talipot-@TalipotVersion@ (x64).lnk" "$INSTDIR\bin\talipot.exe" "" "$INSTDIR\share\talipot\bitmaps\talipot.ico"
  CreateShortCut "$SMPROGRAMS\Talipot-@TalipotVersion@ (x64)\Uninstall.lnk" "$INSTDIR\Uninstall.exe"
  CreateShortCut "$DESKTOP\Talipot-@TalipotVersion@ (x64).lnk" "$INSTDIR\bin\talipot.exe" "" "$INSTDIR\share\talipot\bitmaps\talipot.ico"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Talipot-@TalipotVersion@" "DisplayName" "Talipot-@TalipotVersion@ (x64)"

  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Talipot-@TalipotVersion@" "UninstallString" "$INSTDIR\Uninstall.exe"
  WriteUninstaller "$INSTDIR\Uninstall.exe"

  ${registerExtension} "$INSTDIR\bin\talipot.exe" ".tlp" "Talipot.File" "Talipot Graph File" "$INSTDIR\share\talipot\bitmaps\talipot-file-icon.ico"
  ${registerExtension} "$INSTDIR\bin\talipot.exe" ".tlpb" "Talipot.BinaryFile" "Talipot Graph Binary File" "$INSTDIR\share\talipot\bitmaps\talipot-file-icon.ico"
  ${registerExtension} "$INSTDIR\bin\talipot.exe" ".tlpz" "Talipot.File.Gzip" "Talipot Graph File (gzipped)" "$INSTDIR\share\talipot\bitmaps\talipot-file-icon.ico"
  ${registerExtension} "$INSTDIR\bin\talipot.exe" ".tlpbz" "Talipot.BinaryFile.Gzip" "Talipot Graph Binary File (gzipped)" "$INSTDIR\share\talipot\bitmaps\talipot-file-icon.ico"
  ${registerExtension} "$INSTDIR\bin\talipot.exe" ".tlpx" "Talipot.ProjectFile" "Talipot Project File" "$INSTDIR\share\talipot\bitmaps\talipot-file-icon.ico"

  Call RefreshShellIcons

SectionEnd

Section "Uninstall"
  SetShellVarContext all
  SetRegView 64
  Delete "$DESKTOP\Talipot-@TalipotVersion@ (x64).lnk"
  RMDir /r "$SMPROGRAMS\Talipot-@TalipotVersion@ (x64)"
  RMDir /r "$INSTDIR"
  Delete $INSTDIR\Uninstall.exe
  DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Talipot-@TalipotVersion@"

  ${unregisterExtension} ".tlp" "Talipot.File"
  ${unregisterExtension} ".tlpb" "Talipot.BinaryFile"
  ${unregisterExtension} ".tlpz" "Talipot.File.Gzip"
  ${unregisterExtension} ".tlpbz" "Talipot.BinaryFile.Gzip"
  ${unregisterExtension} ".tlpx" "Talipot.ProjectFile"

  Call un.RefreshShellIcons

SectionEnd
